{
  parserClass="org.onj.language.OnjParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Onj"
  psiImplClassSuffix="Impl"
  psiPackage="org.onj.language.psi"
  psiImplPackage="org.onj.language.psi.impl"

  elementTypeHolderClass="org.onj.language.psi.OnjTypes"
  elementTypeClass="org.onj.language.psi.OnjElementType"
  tokenTypeClass="org.onj.language.psi.OnjTokenType"
  extends(".*expr")=expr
}


OnjFile ::= ((objectEntry COMMA) | topLevelStructure)* objectEntry? topLevelStructure?

topLevelStructure ::= importStructure | variableStructure | useStructure

importStructure ::= IMPORT (string | literalExpression) "as" IDENTIFIER SEMICOLON
variableStructure ::= VAR IDENTIFIER EQUALS expression SEMICOLON
useStructure ::= USE IDENTIFIER SEMICOLON

keyValuePair ::= key COLON expression
key ::= (IDENTIFIER | string)
tripleDot ::= DOT DOT DOT literalExpression

objectEntry ::= tripleDot | keyValuePair
arrayEntry ::= tripleDot | expression

expression ::= infixFunctionCallExpression
infixFunctionCallExpression ::= binaryExpression (functionName infixFunctionCallExpression)?
binaryExpression ::= typeConversionExpression (binaryOperator binaryExpression)?
typeConversionExpression ::= unaryExpression (HASH IDENTIFIER)*
unaryExpression ::= (MINUS unaryExpression) | variableAccessExpression
variableAccessExpression ::= literalExpression (DOT literalExpression)*
literalExpression ::=   functionCallExpression |
                        INTEGER |
                        FLOAT |
                        groupedExpression |
                        object |
                        namedObject |
                        array |
                        variableExpression |
                        string


variableExpression ::= IDENTIFIER
functionCallExpression ::= functionName L_PAREN (expression COMMA)* expression? R_PAREN
functionName ::= IDENTIFIER
groupedExpression ::= L_PAREN expression R_PAREN

object ::= L_BRACE (objectEntry COMMA)* objectEntry? R_BRACE
namedObject ::= DOLLAR namedObjectName object
namedObjectName ::= IDENTIFIER
array ::= L_BRACKET (arrayEntry COMMA)* arrayEntry? R_BRACKET

binaryOperator ::= PLUS | MINUS | STAR | DIV
string ::= STRING_BEGIN (STRING_ESCAPE | INVALID_STRING_ESCAPE | STRING_PART)* STRING_END

private comment_ ::= LINE_COMMENT | BLOCK_COMMENT